#!/usr/bin/env python

Import("env")

env_metal = env.Clone()
if "-fno-exceptions" in env_metal["CXXFLAGS"]:
    env_metal["CXXFLAGS"].remove("-fno-exceptions")
if "-std=gnu++17" in env_metal["CXXFLAGS"]:
    env_metal["CXXFLAGS"].remove("-std=gnu++17")
env_metal.Append(CXXFLAGS=["-fexceptions", "-std=c++20"])

thirdparty_obj = []
thirdparty_dir = "#thirdparty/spirv-cross"

env_metal.Prepend(CPPPATH=[thirdparty_dir, thirdparty_dir + "/include"])

# Godot source files

driver_obj = []

# Driver source files
env_metal.add_source_files(
    driver_obj,
    [
        "metal_device_properties.mm",
        "metal_objects.mm",
        "rendering_context_driver_metal.mm",
        "rendering_device_driver_metal.mm",
    ],
)
# Additional flags for pixel_formats.mm
env_metal_tmp = env_metal.Clone()
env_metal_tmp.Append(CXXFLAGS=["-Wno-deprecated-declarations"])
env_metal_tmp.add_source_files(
    driver_obj,
    [
        "pixel_formats.mm",
    ],
)

env_metal.drivers_sources += driver_obj
